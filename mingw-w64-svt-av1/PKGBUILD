# Maintainer: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=SVT-AV1
pkgbase=mingw-w64-svt-av1-git
pkgname=("${MINGW_PACKAGE_PREFIX}-svt-av1-git")
pkgver=0.9.1git
pkgrel=1
pkgdesc="Scalable Video Technology AV1 encoder and decoder (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://gitlab.com/AOMediaCodec/SVT-AV1'
license=('AOM')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-yasm"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${url}/-/archive/master/SVT-AV1-master.tar.gz"
        #"0001-cmake-fix-install-dir.patch"
        )
sha256sums=('75f3c159b8c8b869eca15e9d28eec8f30022865a93a2d5cf8a02b4e8f2cb8536'
            #'b8a32605983003cc2a2380265154095cdb12d62978c63b24e60ee8cee9847762'
           )

prepare() {
  cd "${srcdir}"/SVT-AV1-master

  # https://gitlab.com/AOMediaCodec/SVT-AV1/-/merge_requests/1812
  #patch -p1 -i "${srcdir}/0001-cmake-fix-install-dir.patch"
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DREPRODUCIBLE_BUILDS=ON \
      ../SVT-AV1-master

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}"/build-${CARCH}

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --build . --target install

  install -Dm644 ${srcdir}/SVT-AV1-master/LICENSE.md ${pkgdir}${MINGW_PREFIX}/share/licenses/svt-av1/LICENSE.md
  install -Dm644 ${srcdir}/SVT-AV1-master/PATENTS.md ${pkgdir}${MINGW_PREFIX}/share/licenses/svt-av1/PATENTS.md
}
