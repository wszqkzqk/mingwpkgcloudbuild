# Maintainer: wszqkzqk <wszqkzqk@qq.com>
_realname=oh-my-posh
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=12.6.5
pkgrel=1
pkgdesc="A prompt theme engine for any shell."
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/JanDeDobbeleer/oh-my-posh"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-go")
sha256sums=('c7d4633d443d1fa2ea5a9d5efcad784bba123c6a6d94da3b2330b51be94bcbf7')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
build() {
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

    cd "$pkgname-$pkgver/src"
    go build
}
package() {
    cd "$pkgname-$pkgver/src"
    install -Dm 755 ./oh-my-posh -t "${pkgdir}${MINGW_PREFIX}/bin/"
    install -Dm 644 "../COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkgname}/LICENSE"
    install -d "${pkgdir}${MINGW_PREFIX}/share/oh-my-posh/themes"
    install -m 644 ../themes/* -t "${pkgdir}${MINGW_PREFIX}/share/oh-my-posh/themes"
}